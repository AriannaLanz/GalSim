# vim: set filetype=python :

import os
import glob

Import('env')

libs=['galsim']

env1 = env.Clone(CPPDEFINES=[],LIBS=libs+env['LIBS'])

env1['OBJPREFIX'] = '.obj/'

bin_dir = 'bin'
bin_install_dir = os.path.join(env['INSTALL_PREFIX'], bin_dir)

# Include the library location within the executable.
env1.Append( RPATH = Dir('#.').abspath )
# Workaround for SCons bug: The above doesn't always work correctly.
# RPATH clashes with LINKFLAGS.  So if LINKFLAGS is already set, we need this workaround.
# See: http://scons.tigris.org/issues/show_bug.cgi?id=1644
# Fixed in version 2.1, but leave this next line until scons 2.1 is the version in fink.
env1.Append( LINKFLAGS = ["$__RPATH"] )

sbdraw = env1.Program(os.path.join('#bin','sbdraw'), 'SBDraw.cpp')

shera = env1.Program(os.path.join('#bin','shera'), 'Shera.cpp')

int_example = env1.Program(os.path.join('#bin','int_example'), 'IntExample.cpp')

example_targets = [ sbdraw, shera, int_example ]

env.Alias(target='examples',source=example_targets)

# I think we don't actually want to install the example programs.
# But here is how we would do it for real programs that the user will want
# to have installed:
if 'install' in COMMAND_LINE_TARGETS:

    installed_bin = env1.Install(dir=bin_install_dir, source=bin_targets)
    env1.Alias(target='install', source=installed_bin)


if 'uninstall' in COMMAND_LINE_TARGETS:
    # There is no env.Uninstall method, we must build our own
    deltarget = Delete("$TARGET")

    binfiles = [os.path.join(bin_install_dir, os.path.basename(str(f[0]))) for f in bin_targets]

    for f in binfiles:
        env1.Alias('uninstall', env1.Command(f, None, deltarget))


